
EXTERNAL CustomEvent(string event)
 
 == function CustomEvent(string event) ==
 ~return

===WATCHERS

#w:right
Hey psst.

Do you know much about this film? #w:right
*Not at all.

    **I've never heard of it.
    #w:right
    I think it's some kind of documentary.
    #w:right
    About hotels.
    #w:right
    Always found hotels quite spooky.
    

        ***I know what you mean.
        #w:right
        Just something not quite right about them.
        #w:right
        I don't like how everyone is so anonymous.


        ***It's not a horror film is it?
        #w:right
        Oh no no I don't think so.
        #w:right
        Just a feeling.
        #w:right
        Just a strange feeling about it, that's all.
        #w:right
        I don't expect it'll be scary, it's just me getting all nervous.
        #w:right
        Haha. Yeah.
        
        ---
    
    **It looks alright though.
    #w:right
    Yeah. Kinda spooky though.
    #w:right
    Always found hotels quite spooky.
    

        ***I know what you mean.
        #w:right
        Just something not quite right about them.
        #w:right
        I don't like how everyone is so anonymous.


        ***It's not a horror film is it?
        #w:right
        Oh no no I don't think so.
        #w:right
        Just a feeling.
        #w:right
        Just a strange feeling about it, that's all.
        #w:right
        I don't expect it'll be scary, it's just me getting all nervous.
        #w:right
        Haha. Yeah.
        
        ---

    **Do you?
    #w:right
    Oh no. No idea really.
    #w:right
    I think I saw maybe that it was some kind of documentary?
    #w:right
    About hotels.
    #w:right
    Or, it's based on a true story. One of the two.
    
    --
    
*A little bit.

    **It's about hotels, right?
    **It's about ghosts, right?
    --
    #w:right
    Oh is it? I didn't realise.
    #w:right
    I thought it was about a car crash? Or something?
    #w:right
    As long as it's not boring though.
    #w:right
    As long as it's not boring I don't mind.
    #w:right
    These kinda things usually are, but I'll keep my fingers crossed.
    #w:right
    I'll watch whatever I can nowadays. Nowadays that's all we can do.
           
-
#w:left
Mmm.

#w:right
Feels weird though, doesn't it? Sitting out here like this.

*Yeah. I don't feel safe.
    #w:right
    I'm pretty sure they cautioned off the road.
    #w:right
    Like there's no other traffic around.
    #w:right
    Plus it's the middle of the night.
    **Still, a bit creepy.
    
    **Still, a bit unnerving.
    
    **Still, a bit eerie.
    --
    #w:right
    Yeah, I know what you mean.
    #w:right
    Feels like we're moments away from a disaster.
    
*I think it's quite cool.
    #w:right
    It is different that's for sure.
    #w:right
    Reminds me of some of that experimental theatre we saw back in the day. 
    #w:right
    Do you remember that?
    **Oh, yeah.
    **What, no?
    --
    #w:right
    That ballet in the service station. That weird thing we watched from someone's window overlooking that street.
    #w:right
    Maybe it's all by the same artist I'm not sure.
    
*What do you mean?
    #w:right
    Like... being just parked up in the middle of the motorway like this.
    #w:right
    It's quite frightening. Y'know?
    **Yeah, exposing.
    **Yeah, eerie.
    **Yeah, exciting.
    --
      #w:right
    Just us and the street lights and the crickets and the night.
    #w:right
    And the film of course.
-
#w:left
Mmm.
#w:right
Wait shh shh I think it's starting.
{CustomEvent("START_MOVIE")}
->DONE

===WATCHERS_BORED

#w:right
...Hey psst.
#w:left
What?
#w:right
Can we talk? I'm kinda bored.

*Sure. I'm kinda bored too.
#w:right
Phew I was worried I was the only one.
#w:right
I don't get it. Nothing's happening.
    
    **Yeah I dunno, I thought it would be better.
    #w:right
    Me too. I guess we should've researched more?
    #w:left
    I don't know it's always a risk with these things.
    **Maybe that's the point?
    #w:right
    So what though? If the point of it is to be boring, then sure it's succeeded, but I'm still bored.
    #w:left
    Yeah I get you.
    --
    #w:right
    At least it's a beautiful night.
    **Mmm... I'm kinda cold.
    #w:right
    Let's turn the heat up a bit then, you've got the air con turned all the way down!
    #w:right
    Keep it like a freezer in here.

    **Mmm... I'm a bit warm.
    #w:right
    Let's turn the air con up a bit then, you've got it all the way to the hot side.
    --
    #w:left
    Yeah, sorry.
    #w:right
    What are you sorry about? Don't be silly.
    #w:right
    I just mean like. Even though it kinda sucks it's at least nice to be out here in a place like this.
    #w:right
    Y'know?
    #w:right
    Like you never get to take in a place like this. Always passing through, never staying.
    #w:right
    Would be a good location for a detective story or something.
    #w:left
    Mmm.


*I'm trying to watch it.
#w:right
Oh. Sorry. I'll be quiet.

-

->DONE

===WATCHERS_WINDOW

#w:right
I can't see anything out that window, it's too foggy. 

*Me either.

*Yeah, it's creepy.
-
#w:right
Like a room separate from reality.

->DONE

===WATCHERS_BATHROOM
#w:right
I hate hotel bathrooms, they're so disgusting.

*Not the fancy ones.
#w:right
No, no.
*I agree.
-
#w:right
Even the ones that pretend to be fancy have that same feel to them.
#w:right
That there's been hundreds of other people using it before you.
#w:right
No matter how well cleaned it still feels that way.
#w:right
Like sharing someone else's bar soap.
->DONE

===WATCHERS_BED
#w:right
I can never dream in hotels.
#w:right
I can only dream when I'm at home.
*I'm the same.
#w:right
When I move it takes me a while to start dreaming again, no matter where.
#w:left
Even in our new flat?
#w:right
Especially in our new flat.
*I always have nightmares.
#w:right
Hotel nightmares?
#w:left
Yeah.
#w:right
I don't envy you.
#w:left
I don't wanna talk about it.
-
->DONE

===WATCHERS_FIRST_LOOP

#w:right
Wait what. Is it just looping now?
#w:right
Haven't we seen this bit before?

*It looks like it.

*No, it's a bit different I think.

#w:right
Is it?

#w:left
Yeah there's some little differences I think.

*I can't tell.

#w:right
I feel like this is just the same as earlier.

-
#w:right
Hmm.
#w:right
It's definitely the same room isn't it.
#w:right
But I can't tell if it's the same ghuest.
#w:right
They're doing pretty much the same things.
#w:left
Yeah.
#w:right
It's quite frustrating.
#w:right
It's a shame they can't hear us.
#w:left
Mmm.
->DONE

===WATCHERS_SECOND_LOOP

#w:right
It feels like it's just gonna keep looping like this, doesn't it?

*Yeah, it does.

*What a weird film.
-
#w:right
Can we go home now?
#w:left
Yeah sure.
#w:right
Thank you.
#w:right
Really can't wait to get back after watching this. See our bed and our furniture and everything again.
#w:right
You know what I mean?
#w:left
Yeah, weirdly homesick.
#w:right
Yeah, like. I don't know... Can't put my finger on it.

*Yeah, something just feels really weird.

*Yeah, I feel kinda uncomfortable.
-
#w:left
Like getting lost in a place I once knew.
#w:right
Yeah.
#w:right
When we get home can we have the rest of that pizza?

*I thought you finished it earlier?

*I'm not really in the mood anymore.

-

{CustomEvent("TRIGGER_ENDING")}
//cut to shader full screen version of hotel
->DONE

